
(* @NESTEDCOMMENTS := 'Yes' *)
(* @PATH := '\/05_Basic_Calc' *)
(* @OBJECTFLAGS := '0, 8' *)
(* @SYMFILEFLAGS := '2048' *)
FUNCTION set_poti_value : INT
(* Функция задания калибровочного значения потенциометра по его минимальному и максимальному значениям *)
(* Функция возвращает -1, если введены не верные входные параметры *)
VAR_INPUT
	in_min_mV_s16		: INT;		(* Минимальное значение потенциометра, мВ *)
	in_max_mV_s16		: INT;		(* Максимальное значение потенциометра, мВ *)
	U_lim_mV_s16		: INT;		(* Максимально допустимое напряжение на входе, мВ *)
END_VAR
VAR
	U_half_lim_s16		: INT;		(* Половина диапазона допустимого напряжения*)
	dig_min_raw_s16	: INT;		(* Сконвертированное минимальное значение потенциометра, [0; 255] *)
	dig_max_raw_s16	: INT;		(* Сконвертированное максимальное значение потенциометра, [256; 511] *)
	dig_min_s16			: WORD;		(* Сконвертированное мин. значение потенциометра *)
	dig_max_s16			: WORD;		(* Сконвертированное макс. значение потенциометра *)
END_VAR
(* @END_DECLARATION := '0' *)
	(* Получение границ диапазона для минимального и максимального значения потенциометра *)
	U_half_lim_s16 := U_lim_mV_s16 / 2;

		(* Проверка на правильность входных данных *)
	IF in_min_mV_s16 > U_half_lim_s16	OR
		in_max_mV_s16 < U_half_lim_s16	THEN

		(* Входные данные не верные *)
		set_poti_value := -1;
	ELSE
		(* Входные данные верные *)

		(* Конвертация значений потенциометра в цифровые значения *)
		dig_min_raw_s16 := conv_mV_to_dig(in_min_mV_s16, U_lim_mV_s16);
		dig_max_raw_s16 := conv_mV_to_dig(in_max_mV_s16, U_lim_mV_s16);

		(* Проверка выполнения конвертации *)
		IF dig_min_raw_s16 = -1 OR
			dig_max_raw_s16 = -1 THEN
			(* Конвертация не выполнена, входные данные не верные *)
			set_poti_value := -1;
		ELSE
			(* Конвертация выполнена *)

			(* Пересчет калибровочного значения *)
			dig_min_s16		:= SHL((INT_TO_WORD(dig_min_raw_s16) AND 16#00FF), 8);
			dig_max_s16		:= (INT_TO_WORD(511 - dig_max_raw_s16) AND 16#00FF);

			(* Получение готового калибровочного значения потенциометра *)
			set_poti_value	:= WORD_TO_INT(dig_min_s16 OR dig_max_s16);
		END_IF
	END_IF
END_FUNCTION
